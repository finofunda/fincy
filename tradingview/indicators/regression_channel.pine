// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Copywright © 2026 ZenithClown (Debmalya Pramanik) | https://zenithclown.github.io/ | https://www.linkedin.com/in/dpramanik/
//
//@version=6
//
// ==================================================================
// OBJECTTIVE: Create a Regression Channel & Forward Forecast
//
// A regression channel (linear, exponential, etc.) can potentially
// show the movement of the market over a period of time. Typically
// an instrument move in a direction thus a linear regression can be
// useful to predict the potential flow, while exponential channel can
// be useful for a crypto currency like asset.
//
// Check https://github.com/finofunda/fincy for additional details.
// Check https://in.tradingview.com/u/ZenithClown/ for TV Profile.
// ==================================================================

indicator('Regression Channel', overlay = true)

// ------------------------------------------------------------------
// INPUT(s) for Regression Channel Calculation(s)
// ------------------------------------------------------------------

regressionType = input.string(
    'linear', 'Regression Type'
    , options = [
        'linear', 'exponential', 'polynomial'
    ]
)

dataSource = input.source(close, 'Data Source')

// Lookback and Forecast Period Settings

lookbackPeriod = input.int(
    100, 'Lookback Period (No. of Candels)', minval = 2, step = 1
    , group = 'Model Settings'
    , tooltip = 'Setting a value >= 5000 may result in a slow performance.'
)

// Channel Settings, allow dynamic upper and lower channel controls

showUpperChannel = input.bool(
    true, 'Show Upper Deviation Channel'
    , group = 'Channel Settings', inline = 'Upper Channel'
)

upperChannelDeviation = input.float(
    2.0, '', active = showUpperChannel, step = 0.1
    , group = 'Channel Settings', inline = 'Upper Channel'
)

showLowerChannel = input.bool(
    true, 'Show Lower Deviation Channel'
    , group = 'Channel Settings', inline = 'Lower Channel'
)

lowerChannelDeviation = input.float(
    2.0, '', active = showLowerChannel, step = 0.1
    , group = 'Channel Settings', inline = 'Lower Channel'
)

nTopBottom = input.int(
    2, 'No. of Channel(s) from Mean Line', minval = 1, maxval = 4, step = 1
    , tooltip = 'Create multiple channels to understand market range.'
    , group = 'Channel Settings'
)

// ------------------------------------------------------------------
// Display Setting(s) for Forecast Period & Color(s)
//
// Display settings are defined of two types - general settings which
// are applicable for all (ex. color settings) and regression model
// specific settings. Check the group names for more details.
// ------------------------------------------------------------------

isLinearModel = regressionType == 'linear' ? true : false

showPearsonInput = input.bool(
    false, 'Show Pearsons R', active = isLinearModel
    , group = 'Display Settings (Linear Model)'
)

extendLeftInput = input.bool(
    false, 'Extend Left Lines', active = isLinearModel
    , group = 'Display Settings (Linear Model)'
    , tooltip = 'Extend to check when there was a reversal.'
)

extendRightInput = input.bool(
    true, 'Extend Right Lines', active = isLinearModel
    , group = 'Display Settings (Linear Model)'
    , tooltip = 'Extend to check potential market movement.'
)

linRegExtendType = switch
    extendLeftInput and extendRightInput => extend.both
    extendLeftInput => extend.left
    extendRightInput => extend.right
    => extend.none

// General Display - Color Settings

colorUpperChannel = input.color(
    color.new(color.green, 85), ''
    , group = 'Display Settings', inline = 'Display Settings'
)

colorLowerChannel = input.color(
    color.new(color.red, 85), ''
    , group = 'Display Settings', inline = 'Display Settings'
)

// ------------------------------------------------------------------
// Function(s)/Calculation(s) for Linear Regression & Deviation
//
// linearRegression - Function to calculate linear regression values,
// namely the slope and intercept. The function uses `barstate.islast`
// to prevent the model to recalculate on every candel (thus reducing
// unnecessary load time) and calculates only when we've reached the
// end of the loop.
// ------------------------------------------------------------------

linearRegression(source, length) =>
    if not barstate.islast
        [float(na), float(na), float(na)]
    else
        sumX = 0.0
        sumY = 0.0
        sumXY = 0.0
        sumXSqr = 0.0

        for i = 0 to length - 1 by 1
            value = source[i]
            perct = i + 1.0

            sumX += perct
            sumY += value
            sumXY += perct * value
            sumXSqr += math.pow(perct, 2)

        slope = (length * sumXY - sumX * sumY) / (length * sumXSqr - math.pow(sumX, 2))

        average = sumY / length
        intercept = average - slope * sumX / length + slope

        [slope, average, intercept]

linearDeviation(source, length, slope, average, intercept) =>
    upperDeviation = 0.0
    lowerDeviation = 0.0

    standardDeviationAccum = 0.0

    dsxx = 0.0
    dsyy = 0.0
    dsxy = 0.0

    periods = length - 1
    daY = intercept + slope * periods / 2

    interceptCopy = intercept

    for i = 0 to periods by 1
        price = high[i] - interceptCopy

        if price > upperDeviation
            upperDeviation := price

        price := interceptCopy - low[i]

        if price > lowerDeviation
            lowerDeviation := price

        price := source[i]
        dxt = price - average
        dyt = interceptCopy - daY

        price -= interceptCopy
        dsxx += math.pow(dxt, 2)
        dsyy += math.pow(dyt, 2)
        dsxy += dxt * dyt

        interceptCopy += slope
        
    pearsonsR = dsxx == 0 or dsyy == 0 ? 0 : dsxy / math.sqrt(dsxx * dsyy)
    standardDeviation = ta.stdev(source, length)
    [standardDeviation, pearsonsR, upperDeviation, lowerDeviation]

// ------------------------------------------------------------------
// Visualization of Regression, Forecast & Top/Bottom Bounds
// ------------------------------------------------------------------

labelPrefix = switch regressionType
    'linear' => 'L'
    'exponential' => 'E'
    => na

switch regressionType
    'linear' =>
        [slope, average, intercept] = linearRegression(dataSource, lookbackPeriod)

        // ? Calculate the start and end price of the mean regression line
        startPriceLinReg = intercept + slope * (lookbackPeriod - 1)
        endPriceLinReg = intercept

        var line meanLineLinReg = na
        if na(meanLineLinReg) and not na(startPriceLinReg)
            meanLineLinReg := line.new(
                bar_index - lookbackPeriod + 1, startPriceLinReg, bar_index, endPriceLinReg
                , width = 1, extend = linRegExtendType, color = color.new(color.black, 0)
            )
        else
            line.set_xy1(meanLineLinReg, bar_index - lookbackPeriod + 1, startPriceLinReg)
            line.set_xy2(meanLineLinReg, bar_index, endPriceLinReg)

        [standardDeviation, pearsonsR, upperDeviation, lowerDeviation] = linearDeviation(
            dataSource, lookbackPeriod, slope, average, intercept
        )

        var array<line> upperChannels = array.new<line>(nTopBottom)
        var array<line> lowerChannels = array.new<line>(nTopBottom)

        for i = 0 to nTopBottom - 1 by 1
            upperMultiplier = (i + 1) * (showUpperChannel ? upperChannelDeviation : 1.0) / nTopBottom
            lowerMultiplier = (i + 1) * (showLowerChannel ? lowerChannelDeviation : 1.0) / nTopBottom

            upperStartPrice = startPriceLinReg + upperMultiplier * standardDeviation
            upperEndPrice = endPriceLinReg + upperMultiplier * standardDeviation

            lowerStartPrice = startPriceLinReg - lowerMultiplier * standardDeviation
            lowerEndPrice = endPriceLinReg - lowerMultiplier * standardDeviation

            if na(array.get(upperChannels, i)) and not na(upperStartPrice)
                array.set(upperChannels, i, line.new(
                    bar_index - lookbackPeriod + 1, upperStartPrice, bar_index, upperEndPrice
                    , width = 1, extend = linRegExtendType, color = color.new(colorUpperChannel, 0)
                ))
            else if not na(array.get(upperChannels, i))
                line.set_xy1(array.get(upperChannels, i), bar_index - lookbackPeriod + 1, upperStartPrice)
                line.set_xy2(array.get(upperChannels, i), bar_index, upperEndPrice)

            if na(array.get(lowerChannels, i)) and not na(lowerStartPrice)
                array.set(lowerChannels, i, line.new(
                    bar_index - lookbackPeriod + 1, lowerStartPrice, bar_index, lowerEndPrice
                    , width = 1, extend = linRegExtendType, color = color.new(colorLowerChannel, 0)
                ))
            else if not na(array.get(lowerChannels, i))
                line.set_xy1(array.get(lowerChannels, i), bar_index - lookbackPeriod + 1, lowerStartPrice)
                line.set_xy2(array.get(lowerChannels, i), bar_index, lowerEndPrice)

        // Show Pearson's R
        var label r = na
        label.delete(r[1])

        if showPearsonInput and not na(pearsonsR)
            r := label.new(
                bar_index - lookbackPeriod + 1, startPriceLinReg
                , str.tostring(pearsonsR, 'R = #.#####')
                , color = color.new(color.white, 100)
                , textcolor = color.new(color.black, 0)
                , size = size.normal, style = label.style_label_up
            )
    => display.none
