// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Copywright © 2026 ZenithClown (Debmalya Pramanik) | https://zenithclown.github.io/ | https://www.linkedin.com/in/dpramanik/
//
//@version=6
//
// ==================================================================
// OBJECTTIVE: Create a Regression Channel & Forward Forecast
//
// A regression channel (linear, exponential, etc.) can potentially
// show the movement of the market over a period of time. Typically
// an instrument move in a direction thus a linear regression can be
// useful to predict the potential flow, while exponential channel can
// be useful for a crypto currency like asset.
//
// Check https://github.com/finofunda/fincy for additional details.
// Check https://in.tradingview.com/u/ZenithClown/ for TV Profile.
// ==================================================================

indicator('Regression Channel', overlay = true)

// ------------------------------------------------------------------
// INPUT(s) for Regression Channel Calculation(s)
// ------------------------------------------------------------------

regressionType = input.string(
    'linear', 'Regression Type'
    , options = [
        'linear', 'exponential'
    ]
)

dataSource = input.source(close, 'Data Source')

// Lookback and Forecast Period Settings

lookbackPeriod = input.int(
    100, 'Lookback Period (No. of Candels)', minval = 2, step = 1
    , group = 'Model Settings'
    , tooltip = 'Setting a value >= 5000 may result in a slow performance.'
)

// Channel Settings, allow dynamic upper and lower channel controls

showUpperChannel = input.bool(
    true, 'Show Upper Deviation Channel'
    , group = 'Channel Settings', inline = 'Upper Channel'
)

upperChannelDeviation = input.float(
    2.0, '', active = showUpperChannel, step = 0.1
    , group = 'Channel Settings', inline = 'Upper Channel'
)

showLowerChannel = input.bool(
    true, 'Show Lower Deviation Channel'
    , group = 'Channel Settings', inline = 'Lower Channel'
)

lowerChannelDeviation = input.float(
    2.0, '', active = showLowerChannel, step = 0.1
    , group = 'Channel Settings', inline = 'Lower Channel'
)

nTopBottom = input.int(
    2, 'No. of Channel(s) from Mean Line', minval = 1, maxval = 4, step = 1
    , tooltip = 'Create multiple channels to understand market range.'
    , group = 'Channel Settings'
)

// ------------------------------------------------------------------
// Display Setting(s) for Forecast Period & Color(s)
//
// Display settings are defined of two types - general settings which
// are applicable for all (ex. color settings) and regression model
// specific settings. Check the group names for more details.
// ------------------------------------------------------------------

isLinearModel = regressionType == 'linear' ? true : false
isExponentialModel = regressionType == 'exponential' ? true : false

// Inputs for Linear Regression Model

showPearsonInput = input.bool(
    false, 'Show Pearsons R', active = isLinearModel
    , group = 'Display Settings (Linear Model)'
)

// General Display - Line Extensions

extendLeftInput = input.bool(
    false, 'Extend Lines towards Left'
    , group = 'Display Settings'
)

extendRightInput = input.bool(
    true, 'Extend Lines towards Right'
    , group = 'Display Settings'
)

useColorGradient = input.bool(
    true, 'Show Color Gradient'
    , group = 'Display Settings'
)

colorUpperChannel = input.color(
    color.new(color.green, 85), ''
    , group = 'Display Settings', inline = 'Display Settings'
)

colorLowerChannel = input.color(
    color.new(color.red, 85), ''
    , group = 'Display Settings', inline = 'Display Settings'
)

// ------------------------------------------------------------------
// Function(s)/Calculation(s) for Linear Regression & Deviation
//
// linearRegression - Function to calculate linear regression values,
// namely the slope and intercept. The function uses `barstate.islast`
// to prevent the model to recalculate on every candel (thus reducing
// unnecessary load time) and calculates only when we've reached the
// end of the loop.
// ------------------------------------------------------------------

linearRegression(source, length) =>
    if not barstate.islast
        [float(na), float(na), float(na)]
    else
        sumX = 0.0
        sumY = 0.0
        sumXY = 0.0
        sumXSqr = 0.0

        for i = 0 to length - 1 by 1
            value = source[i]
            perct = i + 1.0

            sumX += perct
            sumY += value
            sumXY += perct * value
            sumXSqr += math.pow(perct, 2)

        slope = (length * sumXY - sumX * sumY) / (length * sumXSqr - math.pow(sumX, 2))

        average = sumY / length
        intercept = average - slope * sumX / length + slope

        [slope, average, intercept]

linearDeviation(source, length, slope, average, intercept) =>
    upperDeviation = 0.0
    lowerDeviation = 0.0

    standardDeviationAccum = 0.0

    dsxx = 0.0
    dsyy = 0.0
    dsxy = 0.0

    periods = length - 1
    daY = intercept + slope * periods / 2

    interceptCopy = intercept

    for i = 0 to periods by 1
        price = high[i] - interceptCopy

        if price > upperDeviation
            upperDeviation := price

        price := interceptCopy - low[i]

        if price > lowerDeviation
            lowerDeviation := price

        price := source[i]
        dxt = price - average
        dyt = interceptCopy - daY

        price -= interceptCopy
        dsxx += math.pow(dxt, 2)
        dsyy += math.pow(dyt, 2)
        dsxy += dxt * dyt

        interceptCopy += slope
        
    pearsonsR = dsxx == 0 or dsyy == 0 ? 0 : dsxy / math.sqrt(dsxx * dsyy)
    standardDeviation = ta.stdev(source, length)
    [standardDeviation, pearsonsR, upperDeviation, lowerDeviation]

// ------------------------------------------------------------------
// Function(s)/Calculation(s) for Exponential Regression & Deviation
//
// exponentialRegression - Function to calculate exponential
// regression values, which can be used to form a baseline. An
// exponential regression line maybe useful in certain instruments
// like the crypto currencies/forex where volatility is much higher.
// ------------------------------------------------------------------

exponentialRegression(source, length) =>
    if not barstate.islast
        [float(na), float(na), float(na)]
    else
        sumX = 0.0
        sumY = 0.0
        sumXY = 0.0
        sumXSqr = 0.0

        for i = 0 to length - 1 by 1
            value = source[i]
            
            if value > 0
                perct = i + 1.0
                logValue = math.log(value)
                
                sumX += perct
                sumY += logValue
                sumXY += perct * logValue
                sumXSqr += math.pow(perct, 2)

        slope = (length * sumXY - sumX * sumY) / (length * sumXSqr - math.pow(sumX, 2))
        
        average = sumY / length
        intercept = average - slope * sumX / length + slope
        
        [slope, average, intercept]

exponentialDeviation(source, length, slope, average, intercept) =>
    upperDeviation = 0.0
    lowerDeviation = 0.0
    
    standardDeviationAccum = 0.0
    
    periods = length - 1
    
    for i = 0 to periods by 1
        expValue = math.exp(intercept + slope * (periods - i))
        
        price = high[i] - expValue
        
        if price > upperDeviation
            upperDeviation := price
        
        price := expValue - low[i]
        
        if price > lowerDeviation
            lowerDeviation := price
        
        if source[i] > 0
            residual = math.log(source[i]) - (intercept + slope * (periods - i))
            standardDeviationAccum += math.pow(residual, 2)
    
    standardDeviation = math.sqrt(standardDeviationAccum / length)
    
    [standardDeviation, upperDeviation, lowerDeviation]

// ------------------------------------------------------------------
// Visualization of Regression, Forecast & Top/Bottom Bounds
// ------------------------------------------------------------------

extendLineType = switch
    extendLeftInput and extendRightInput => extend.both
    extendLeftInput => extend.left
    extendRightInput => extend.right
    => extend.none

// ? Helper function(s) to draw objects into screen

createUpdateLine(existingLine, x1, y1, x2, y2, lineColor) =>
    if na(existingLine) and not na(y1)
        line.new(
            x1, y1, x2, y2
            , width = 1
            , extend = extendLineType
            , color = lineColor
        )
    else
        line.set_xy1(existingLine, x1, y1)
        line.set_xy2(existingLine, x2, y2)
        existingLine

createChannelLine(channelArray, index, x1, y1, x2, y2, lineColor) =>
    if na(array.get(channelArray, index)) and not na(y1)
        array.set(
            channelArray, index
            , line.new(
                x1, y1, x2, y2
                , width = 1
                , extend = extendLineType
                , color = color.new(lineColor, 0)
            )
        )

createUpdateFill(
    fillArray, index, baseLine, channelLine, fillColor, transparency
) =>
    if not na(channelLine) and not na(baseLine)
        if na(array.get(fillArray, index))
            array.set(fillArray, index, linefill.new(baseLine, channelLine, color.new(fillColor, transparency)))
        else
            linefill.set_color(array.get(fillArray, index), color.new(fillColor, transparency))

// ? Draw On Graph - Based on Regression Type

switch regressionType
    'linear' =>
        [slope, average, intercept] = linearRegression(dataSource, lookbackPeriod)

        startPriceLinReg = intercept + slope * (lookbackPeriod - 1)
        endPriceLinReg = intercept

        var line meanLineLinReg = na
        meanLineLinReg := createUpdateLine(
            meanLineLinReg
            , bar_index - lookbackPeriod + 1, startPriceLinReg, bar_index, endPriceLinReg
            , color.new(color.black, 0)
        )

        [standardDeviation, pearsonsR, upperDeviation, lowerDeviation] = linearDeviation(
            dataSource, lookbackPeriod, slope, average, intercept
        )

        var array<line> upperChannels = array.new<line>(nTopBottom)
        var array<line> lowerChannels = array.new<line>(nTopBottom)
        var array<linefill> upperFills = array.new<linefill>(nTopBottom)
        var array<linefill> lowerFills = array.new<linefill>(nTopBottom)

        for i = 0 to nTopBottom - 1 by 1
            upperMultiplier = (i + 1) * (showUpperChannel ? upperChannelDeviation : 1.0) / nTopBottom
            lowerMultiplier = (i + 1) * (showLowerChannel ? lowerChannelDeviation : 1.0) / nTopBottom

            upperStartPrice = startPriceLinReg + upperMultiplier * standardDeviation
            upperEndPrice = endPriceLinReg + upperMultiplier * standardDeviation

            lowerStartPrice = startPriceLinReg - lowerMultiplier * standardDeviation
            lowerEndPrice = endPriceLinReg - lowerMultiplier * standardDeviation

            createChannelLine(
                upperChannels, i
                , bar_index - lookbackPeriod + 1, upperStartPrice, bar_index, upperEndPrice
                , colorUpperChannel
            )
            
            if useColorGradient
                gradientTransparency = int(85 + (100 - 85) * (i + 1) / nTopBottom)
                baseLine = i == 0 ? meanLineLinReg : array.get(upperChannels, i - 1)
                createUpdateFill(
                    upperFills, i
                    , baseLine, array.get(upperChannels, i)
                    , colorUpperChannel, gradientTransparency
                )

            createChannelLine(
                lowerChannels, i
                , bar_index - lookbackPeriod + 1, lowerStartPrice, bar_index, lowerEndPrice
                , colorLowerChannel
            )
            
            if useColorGradient
                gradientTransparency = int(85 + (100 - 85) * (i + 1) / nTopBottom)
                baseLine = i == 0 ? meanLineLinReg : array.get(lowerChannels, i - 1)
                createUpdateFill(
                    lowerFills, i
                    , baseLine, array.get(lowerChannels, i)
                    , colorLowerChannel, gradientTransparency
                )

        var label r = na
        label.delete(r[1])

        if showPearsonInput and not na(pearsonsR)
            r := label.new(
                bar_index - lookbackPeriod + 1, startPriceLinReg
                , str.tostring(pearsonsR, 'R = #.#####')
                , color = color.new(color.white, 100)
                , textcolor = color.new(color.black, 0)
                , size = size.normal, style = label.style_label_up
            )

    'exponential' =>
        [slope, average, intercept] = exponentialRegression(dataSource, lookbackPeriod)

        startPriceExpReg = math.exp(intercept + slope * (lookbackPeriod - 1))
        endPriceExpReg = math.exp(intercept)

        var line meanLineExpReg = na
        meanLineExpReg := createUpdateLine(
            meanLineExpReg
            , bar_index - lookbackPeriod + 1, startPriceExpReg, bar_index, endPriceExpReg
            , color.new(color.black, 0)
        )

        [standardDeviation, upperDeviation, lowerDeviation] = exponentialDeviation(
            dataSource, lookbackPeriod, slope, average, intercept
        )

        var array<line> upperChannelsExp = array.new<line>(nTopBottom)
        var array<line> lowerChannelsExp = array.new<line>(nTopBottom)
        var array<linefill> upperFillsExp = array.new<linefill>(nTopBottom)
        var array<linefill> lowerFillsExp = array.new<linefill>(nTopBottom)

        for i = 0 to nTopBottom - 1 by 1
            upperMultiplier = (i + 1) * (showUpperChannel ? upperChannelDeviation : 1.0) / nTopBottom
            lowerMultiplier = (i + 1) * (showLowerChannel ? lowerChannelDeviation : 1.0) / nTopBottom

            upperStartPrice = math.exp(intercept + slope * (lookbackPeriod - 1) + upperMultiplier * standardDeviation)
            upperEndPrice = math.exp(intercept + upperMultiplier * standardDeviation)

            lowerStartPrice = math.exp(intercept + slope * (lookbackPeriod - 1) - lowerMultiplier * standardDeviation)
            lowerEndPrice = math.exp(intercept - lowerMultiplier * standardDeviation)

            createChannelLine(
                upperChannelsExp, i
                , bar_index - lookbackPeriod + 1, upperStartPrice, bar_index, upperEndPrice
                , colorUpperChannel
            )

            if useColorGradient
                gradientTransparency = int(85 + (100 - 85) * (i + 1) / nTopBottom)
                baseLine = i == 0 ? meanLineExpReg : array.get(upperChannelsExp, i - 1)
                createUpdateFill(
                    upperFillsExp, i
                    , baseLine, array.get(upperChannelsExp, i)
                    , colorUpperChannel, gradientTransparency
                )

            createChannelLine(
                lowerChannelsExp, i
                , bar_index - lookbackPeriod + 1, lowerStartPrice, bar_index, lowerEndPrice
                , colorLowerChannel
            )

            if useColorGradient
                gradientTransparency = int(85 + (100 - 85) * (i + 1) / nTopBottom)
                baseLine = i == 0 ? meanLineExpReg : array.get(lowerChannelsExp, i - 1)
                createUpdateFill(
                    lowerFillsExp, i
                    , baseLine, array.get(lowerChannelsExp, i)
                    , colorLowerChannel, gradientTransparency
                )

    => display.none
